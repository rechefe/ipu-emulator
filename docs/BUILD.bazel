load("@rules_python//python:defs.bzl", "py_binary")
load("@ipu_as_deps//:requirements.bzl", "requirement")

py_binary(
    name = "gen_docs",
    srcs = ["gen_docs_wrapper.py"],
    main = "gen_docs_wrapper.py",
    deps = ["//src/tools/ipu-as-py:ipu_as_lib"],
    python_version = "PY3",
)

genrule(
    name = "generate_instruction_docs",
    outs = ["instructions.md"],
    cmd = "$(location :gen_docs) $(@D)",
    tools = [":gen_docs"],
)

py_binary(
    name = "mkdocs",
    srcs = ["mkdocs_wrapper.py"],
    main = "mkdocs_wrapper.py",
    deps = [
        requirement("mkdocs"),
        requirement("mkdocs-material"),
    ],
    data = [
        "mkdocs.yml",
        "index.md",
        ":generate_instruction_docs",
    ],
    python_version = "PY3",
)

genrule(
    name = "build_docs",
    srcs = [
        "mkdocs.yml",
        "index.md",
        ":generate_instruction_docs",
    ],
    outs = ["site.tar"],
    tags = ["docs"],
    cmd = """
        mkdir -p $(@D)/docs_temp/docs
        cp $(location mkdocs.yml) $(@D)/docs_temp/
        cp $(location index.md) $(@D)/docs_temp/docs/
        cp $(location :generate_instruction_docs) $(@D)/docs_temp/docs/
        ORIGDIR=$$PWD
        cd $(@D)/docs_temp
        $$ORIGDIR/$(location :mkdocs) build --site-dir $$ORIGDIR/$(@D)/site_output
        cd $$ORIGDIR
        tar -cf $@ -C $(@D)/site_output .
    """,
    tools = [":mkdocs"],
)
