load("@ipu_as_deps//:requirements.bzl", "requirement")
load("@ipu_as_pyproject//:deps.bzl", "get_requirements")
load("@rules_python//python:defs.bzl", "py_binary", "py_library", "py_test")

package(default_visibility = ["//visibility:public"])

# Update requirements.txt from pyproject.toml by running:
# bazel run //src/tools/ipu-as-py:update_requirements
exports_files([
    "pyproject.toml",
])

py_library(
    name = "ipu_as_lib",
    srcs = glob([
        "src/ipu_as/*.py",
    ]),
    data = glob([
        "src/ipu_as/**/*.lark",
        "src/ipu_as/**/*.j2",
    ]),
    imports = ["src"],
    deps = get_requirements(requirement),
)

py_binary(
    name = "ipu-as",
    srcs = ["src/ipu_as/cli.py"],
    imports = ["src"],
    main = "src/ipu_as/cli.py",
    deps = [":ipu_as_lib"],
)

py_test(
    name = "test_assemble",
    srcs = ["test/test_assemble.py"],
    imports = ["src"],
    deps = [":ipu_as_lib"],
)

genrule(
    name = "gen_inst_parser",
    outs = [
        "inst_parser.h",
        "inst_parser.c",
    ],
    cmd = "$(location :ipu-as) c-gen --out-dir $(@D)",
    tools = [":ipu-as"],
)

cc_library(
    name = "inst_parser",
    srcs = ["inst_parser.c"],
    hdrs = ["inst_parser.h"],
    copts = ["-Wno-unused-variable"],
)
